* Equilibration simulation using HyResII
*

if @?toppar eq 0     set toppar = ./
if @?segid  eq 0     set segid = pept

read  rtf card name @toppar/top-idpcg-sasa.inp
read para card name @toppar/param-idpcg-sasa.inp

read psf  card name @segid.psf
read coor pdb  name @segid_ext.pdb

coor stat
coor copy comp

! Nonbond w/ Debye Huckel electrostatics
! Hyres2 default: \eps = 20, IONC = 0.15 M
!
NBOND atom switch cdie vdw vswitch -
      cutnb 20.0 ctofnb 18.0 ctonnb 16.0 -
      eps 20 ionc 0.15

nbond inbfrq -1

!SASA
!----------------------sasa-------------------------------
! only applied to the backbone w/ retuned \sigma values 
!
define bb sele type N .or. type NH1 .or. type O -
            .or. type C .or. type C1E .or. type C2E -
            .or. type C3E .or. type CT show end

sasa  sele bb end S014 -0.01 S017 -0.01 S021 -0.01 

energy

! Langevin dynamics equilibration simulation
!
! MMFP to prevent peptide from drifting away
mmfp
   geo sphere rcm force 1.0 droff 0.0 xref 0.0 yref 0.0 zref 0.0 -
       select all end
end

shake tolerance 1.0e-06 bonh param
scalar FBETA set 0.2 select .not. hydrogen end

set nstep = 100000 !100 ps

system "mkdir rst dcd pdb"
open writ unit 12 card name rst/eq.rest
open writ unit 13 file name dcd/eq.dcd

DYNAMICS LEAP LANGEVIN start timestep 0.002 nstep @nstep -
     iunread -1 iunwri 12 iuncrd 13 kunit -1 iunvel -1 -
     nprint 500 iprfrq 5000 nsavc 500 nsavv 0 isvfrq 5000 -
     firstt 300.0 finalt 300.0 tbath 300.0 rbuf 0.0

writ coor pdb name pdb/eq.pdb
* equibrated pdb
*    
  
stop
